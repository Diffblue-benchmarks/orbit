/*
 Copyright (C) 2017 Electronic Arts Inc.  All rights reserved.

 Redistribution and use in source and binary forms, with or without
 modification, are permitted provided that the following conditions
 are met:

 1.  Redistributions of source code must retain the above copyright
     notice, this list of conditions and the following disclaimer.
 2.  Redistributions in binary form must reproduce the above copyright
     notice, this list of conditions and the following disclaimer in the
     documentation and/or other materials provided with the distribution.
 3.  Neither the name of Electronic Arts, Inc. ("EA") nor the names of
     its contributors may be used to endorse or promote products derived
     from this software without specific prior written permission.

 THIS SOFTWARE IS PROVIDED BY ELECTRONIC ARTS AND ITS CONTRIBUTORS "AS IS" AND ANY
 EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
 WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
 DISCLAIMED. IN NO EVENT SHALL ELECTRONIC ARTS OR ITS CONTRIBUTORS BE LIABLE FOR ANY
 DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
 (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
 LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
 ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
 THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

def configureProject(final String projectName, final List<List<Project>> toAdd) {
    toAdd.each {
        it.add(project(projectName))
    }
}

buildscript {
    ext.kotlinVersion = getProperty("kotlin.version")
    ext.junitPlatformVersion = "1.0.1"

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
        classpath("org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion")
        classpath("org.junit.platform:junit-platform-gradle-plugin:$junitPlatformVersion")
    }
}

// Global Properties
ext {
    kotlinVersion = "1.2.0"
    orbitGroup = "cloud.orbit"
    orbitVersion = getProperty("orbit.version")
    orbitCodename = getProperty("orbit.codename")
    orbitCompat = getProperty("orbit.compat")
    orbitUrl = "http://www.orbit.cloud"
    orgName = "Electronic Arts"
    orgUrl = "http://www.ea.com"

    javaTargetVersion = "1.8"
    javaSourceVersion = "1.8"
    kotlinTargetVersion = "1.8"

    junitJupiterVersion = "5.0.1"
}

// Project Configurations
def withKotlin = []
def withJava = []
def withTests = []

configureProject(":framework:orbit-core", [withKotlin, withJava, withTests])
configureProject(":framework:orbit-concurrent", [withKotlin, withJava, withTests])

allprojects {
    group =  orbitGroup
    version = orbitVersion

    repositories {
        mavenCentral()
    }
}

// Kotlin
configure(withKotlin) {
    apply plugin: "kotlin"

    compileKotlin {
        kotlinOptions {
            jvmTarget = kotlinTargetVersion
        }
    }

    dependencies {
        compile("org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion")
        compile("org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion")
    }
}

// Java
configure(withJava) {
    apply plugin: "java"

    compileJava   {
        sourceCompatibility = javaSourceVersion
        targetCompatibility = javaTargetVersion
    }

    jar {
        manifest {
            attributes(
                    "Name": project.name,
                    "Implementation-Title": "$orbitCodename Orbit",
                    "Implementation-Version": orbitVersion,
                    "Implementation-Vendor": orgName,
                    "Specification-Title": "$orbitCodename Orbit",
                    "Specification-Version": orbitCompat,
                    "Specification-Vendor": orgName
            )
        }
    }
}

// Tests
configure(withTests) {
    apply plugin: "org.junit.platform.gradle.plugin"

    dependencies {
        testCompile("org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion")
        testRuntime("org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion")
    }
}